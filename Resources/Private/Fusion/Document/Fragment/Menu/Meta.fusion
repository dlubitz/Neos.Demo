##
# The footer meta navigation is a globally cached fragment that is used on every page.
# To make global caching possible the renderer is separated and instance on the root level.
# The component just renders this globally defined instance.
# This is necessary as the fusion path is part of the cache identifier and would include
# the current document if it's included directly without the global instance.
#
prototype(Neos.Demo:Document.Fragment.Menu.Meta.Renderer) < prototype(Neos.Fusion:Component) {
    menuItems = ${q(site).property('metaNavigationItems')}

    renderer = afx`
        <nav @if.hasMenuItems={props.menuItems} class="nav" role="navigation">
            <ul class="nav nav-pills">
                <Neos.Fusion:Loop items={props.menuItems}>
                    <li>
                        <Neos.Neos:NodeLink node={item}>{item.label}</Neos.Neos:NodeLink>
                    </li>
                </Neos.Fusion:Loop>
            </ul>
        </nav>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            global = 'metaMenu'
            site = ${site}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTag(site)}
        }
    }
}

prototype(Neos.Demo:Document.Fragment.Menu.Meta) < prototype(Neos.Fusion:Renderer) {
    renderPath = '/metaMenu'
}

metaMenu = Neos.Demo:Document.Fragment.Menu.Meta.Renderer
