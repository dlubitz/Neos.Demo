prototype(Neos.Demo:Presentation.Block.Carousel) < prototype(Neos.Neos:ContentComponent) {
    identifier = ''
    carouselItems = null
    carouselItemNumber = 0

    renderer = afx`
        <div>
            <div class="carousel slide" id={props.identifier}>

                <ol class="carousel-indicators">
                    <Neos.Fusion:Loop items={Array.range(1, props.carouselItemNumber)} itemName="index" iterationName="iterator">
                        <li
                            data-target={'#' + props.identifier}
                            data-slide-to={iterator.index}
                            class={iterator.isFirst ? 'active' : ''}
                        ></li>
                    </Neos.Fusion:Loop>
                </ol>

                {props.carouselItems}

                <a class="left carousel-control" href={'#' + props.identifier} data-slide="prev">
                    <span class="icon-prev"></span>
                </a>
                <a class="right carousel-control" href={'#' + props.identifier} data-slide="next">
                    <span class="icon-next"></span>
                </a>
            </div>
        </div>
    `
}
