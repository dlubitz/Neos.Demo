prototype(Neos.Demo:Block.YouTube) < prototype(Neos.Fusion:Component) {
    @styleguide {
        props {
            videoId = '4DVD03IlXIs'
        }
    }

    videoId = null
    inBackend = false

    renderer = afx`
        <figure  @if={props.videoId} class="aspect-video">
            <iframe
                src={'https://www.youtube.com/embed/' + props.videoId + '?wmode=transparent'}
                width="100%"
                height="100%"
                frameborder="0"
                allowfullscreen
            ></iframe>
        </figure>
        <p @if={!props.videoId && props.inBackend} class="aspect-video flex items-center justify-center bg-orange-400 text-white text-xl">
            {I18n.translate('Neos.Demo:NodeTypes.Content.YouTube:error.no.videoid')}
        </p>
    `
}
