prototype(Neos.Demo:Template.Base) < prototype(Neos.Fusion:Component) {
    // This is used for the living styleguide (Monocle)
    // Read more about this in the README.md
    @styleguide {
        props {
            heroImage = ${StaticResource.uri('Neos.Neos', 'Public/Images/dummy-image.svg')}
            heroContent = afx`<Sitegeist.Monocle:Preview.Prototype prototypeName='Neos.Demo:Atom.Headline' props.content='You wonâ€˜t need a manual or training to edit content in Neos - because you already know how to do that.' />`
            header = afx`<Sitegeist.Monocle:Preview.Prototype prototypeName='Neos.Demo:Section.Header'/>`
            //footer = afx`<Sitegeist.Monocle:Preview.Prototype prototypeName='Neos.Demo:Section.Footer' />`
        }
    }

    header = null
    footer = null
    heroContent = null
    heroImage = null
    content = null


    renderer = afx`
        {props.header}
        <div
            @if={props.heroContent || props.heroImage}
            class={['overflow-hidden mb-28 bg-slate-900 flex flex-col', props.heroImage ? 'bg-cover bg-center -mt-[var(--header-height)] h-screen' : 'pt-20 pb-32']}
            style={props.heroImage ? 'background-image: url(' + props.heroImage + ');text-shadow:1px 0 0 rgb(0 0 0 / 50%);' : null}
        >
            {props.heroContent}
        </div>
        {props.hero}
        {props.content}
        {props.footer}
    `
}
