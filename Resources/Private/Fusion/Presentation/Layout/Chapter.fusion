prototype(Neos.Demo:Presentation.Layout.Chapter) < prototype(Neos.Fusion:Component) {
    // This is used for the living styleguide (Monocle)
    // Read more about this in the README.md
    @styleguide {
        props {
            renderDummyImage = true
            breadcrumb = afx`<Sitegeist.Monocle:Preview.Prototype prototypeName='Neos.Demo:Presentation.Breadcrumb' />`
            chapterImageSrc = ${StaticResource.uri('Neos.Neos', 'Public/Images/dummy-image.svg')}
            chapterImageAlt = 'Alternative text'
            content = afx`
                <Sitegeist.Monocle:Preview.Prototype prototypeName='Neos.Demo:Presentation.Text' />
                <Sitegeist.Monocle:Preview.Prototype prototypeName='Neos.Demo:Presentation.Text' />
                <Sitegeist.Monocle:Preview.Prototype prototypeName='Neos.Demo:Presentation.Text' />
            `
           footer = afx`<Sitegeist.Monocle:Preview.Prototype prototypeName='Neos.Demo:Presentation.Footer' />`
        }
    }

    chapterImageSrc = null
    renderDummyImage = false

    _chapterClass = 'block py-3 lg:px-3 lg:py-0'
    _chapterNavClass = 'text-light no-underline hocus:underline'
    renderer = afx`
        <div class="max-w-screen-sm px-4 pt-4 text-justify mx-auto prose prose-lg [&_p:first-of-type:first-letter]:dropcap [&_p:first-of-type:first-letter]:text-light">
            {props.breadcrumb}
            <header class="mb-4">
                <h1 class="text-6xl">{props.title}</h1>
            </header>
            <Neos.Demo:Presentation.Image
                renderDummyImage={props.renderDummyImage}
                src={props.chapterImageSrc}
                alt={props.chapterImageAlt}
                imageClass="max-w-none w-full"
                class="my-3 md:float-right md:ml-4 md:max-w-[250px]"
            />
            <main class="content prose">
                {props.content}
            </main>
        </div>
        <nav @if={props.previousChapterLink || props.nextChapterLink} class="mt-12 px-8 mx-auto divide-y divide-slate-300 flex flex-col flex-wrap justify-center items-center lg:flex-row lg:divide-y-0 lg:divide-x lg:divide-slate-500 print:hidden">
            <a @if={props.previousChapterLink} href={props.previousChapterLink} class={[props._chapterClass, props._chapterNavClass]}>
                {props.previousChapterLabel}
            </a>
            <span class={props._chapterClass}>{props.chapterLabel}</span>
            <a @if={props.nextChapterLink} href={props.nextChapterLink} class={[props._chapterClass, props._chapterNavClass]}>
                {props.nextChapterLabel}
            </a>
        </nav>
        {props.footer}
    `
}
