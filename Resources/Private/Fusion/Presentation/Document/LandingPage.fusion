prototype(Neos.Demo:Presentation.Document.LandingPage) < prototype(Neos.Fusion:Component) {
    mainMenu = ''
    secondaryMenu = ''
    languageMenu = ''
    metaMenu = ''
    breadcrumbMenu = ''

    teaserContent = ''
    mainContent = ''
    footerContent = ''

    imageUri = ''
    imageTitleText = ''

    bodyClass = ''

    renderer = Neos.Demo:Presentation.Document.Base {
        bodyClass = ${props.bodyClass}

        body >
        body = afx`
             <div class="top-navigation-wrap navbar-fixed-top">
                <div class="container">
                    {props.languageMenu}
                    {props.mainMenu}
                    {props.secondaryMenu}
                </div>
            </div>

            <div class={props.imageUri ? 'main-header image' : 'main-header'}
                style={props.imageUri ? 'background-image: url(' + props.imageUri + ');' : null}
                title={props.imageTitleText ? props.imageTitleText : null}
            >
                <div class="container">
                    {props.teaserContent}
                </div>
            </div>

            <div class="container">
                {props.mainContent}
            </div>

            <footer role="navigation" class="navbar navbar-default">
                <div class="panel panel-default">
                    <div class="panel-body clearfix">
                        <div class="container">
                            {props.metaMenu}
                        </div>
                    </div>
                    <div class="panel-footer">
                        <div class="container">
                            {props.footerContent}
                        </div>
                    </div>
                </div>
            </footer>
        `
    }
}
