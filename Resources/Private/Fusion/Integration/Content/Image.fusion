##
# "Image" element
#
prototype(Neos.Demo:Content.Image) < prototype(Neos.Neos:ContentComponent) {
    src = Neos.Fusion:Case {
        asset {
            condition = ${q(node).property('image')}
            renderer = Neos.Neos:ImageUri {
                asset = ${q(node).property('image')}
            }
        }
        dummy {
            condition = ${node.context.inBackend}
            renderer = Neos.Fusion:ResourceUri {
                path = 'resource://Neos.Neos/Public/Images/dummy-image.svg'
            }
        }
    }

    alt = ${q(node).property('alternativeText')}
    title = ${q(node).property('title')}
    class = null

    hasCaption = ${q(node).property('hasCaption')}
    caption = Neos.Neos:Editable {
        property = 'caption'
    }

    renderer = afx`
        <div>
            <Neos.Demo:Presentation.Atom.Image {...props} />
        </div>
    `
}
