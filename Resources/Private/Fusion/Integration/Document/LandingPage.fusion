/**
 *  Special landing page layout with less navigation components
 */
prototype(Neos.Demo:Document.LandingPage) < prototype(Neos.Fusion:Component) {
    mainMenu = Neos.Demo:Document.Fragment.Menu.Main
    secondaryMenu = Neos.Demo:Document.Fragment.Menu.Secondary
    languageMenu = Neos.Demo:Document.Fragment.Menu.Language
    metaMenu = Neos.Demo:Document.Fragment.Menu.Meta
    breadcrumbMenu = Neos.Demo:Document.Fragment.Menu.Breadcrumb

    teaserContent = Neos.Demo:Document.Fragment.Content.Teaser
    mainContent = Neos.Demo:Document.Fragment.Content.Main
    footerContent = Neos.Demo:Document.Fragment.Content.Footer


    imageUri = Neos.Neos:ImageUri {
        asset = ${q(node).property('image')}
        maximumWidth = 2560
        maximumHeight = 1280
        @if.image = ${q(node).property('image')}
    }
    imageTitleText = ${q(node).property('imageTitleText')}

    bodyClass = ${q(node).parents().count() >= 1 && q(node).children('[instanceof Neos.Neos:Document]').filter('[_hiddenInIndex=false]').count() > 0 ? 'has-subpages' : null}

    renderer = afx`
		<Neos.Demo:Presentation.Document.LandingPage {...props}/>
    `
}
