/**
 * BlogPosting rendering for the Neos.Demo website
 */
prototype(Neos.Demo:Document.BlogPosting) < prototype(Neos.Demo:Document.Page) {

    structuredData.blogPosting = Neos.Seo:StructuredData.RootObject {
        type = "BlogPosting"
        attributes = Neos.Fusion:DataStructure {
            url = Neos.Neos:NodeUri {
                node = ${documentNode}
            }
            headline = ${q(node).property('headline')}
            abstract = ${q(node).property('abstract')}
            datePublished = ${Date.format(q(node).property('datePublished'), 'Y-m-d')}
            datePublished.@if.exists = ${q(node).property('datePublished')}
        }
    }

    # content is overwritten to render the intro section first
    content >
    content = Neos.Fusion:Join {

        image = Neos.Neos:ImageTag {
            asset = ${q(node).property('image')}
            maximumWidth = 1000
            maximumHeight = 200
        }

        intro = afx`
            <h1 class="text-center"><Neos.Neos:Editable property="headline" block={false} /></h1>
            <p class="text-center"><Neos.Neos:Editable property="abstract" block={false} /></p>
            <p class="text-center" @if={q(node).property('datePublished')}>{Date.format(q(node).property('datePublished'), 'd.m.Y')}</p>
        `
        intro.@process.contentElementWrapping = Neos.Neos:ContentElementWrapping

        main = Neos.Neos:PrimaryContent {
            nodePath = 'main'
            @process.convertUris = Neos.Neos:ConvertUris
        }
    }
}
