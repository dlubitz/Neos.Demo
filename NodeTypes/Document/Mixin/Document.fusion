prototype(Neos.Demo:Mixin.Document) < prototype(Neos.Fusion:Component) {
    heroContent = null
    heroImage = null
    content = null
    bodyClass = null
    bodyStyles = Neos.Fusion:DataStructure

    header = Neos.Fusion:Renderer {
        renderPath = '/header'
    }


    // footer  = Neos.Fusion:Renderer {
    //     renderPath = '/footer'
    // }

    renderer = Neos.Neos:Page {
        htmlTag.attributes.class = ${String.startsWith(Configuration.setting('Neos.Flow.core.context'), 'Development') ? 'development' : null}
        head {
            stylesheets {
                site = Neos.Demo:Atom.Stylesheet
            }
            javascripts {
                site = Neos.Demo:Atom.Javascript
            }
            metadata = '<meta name="viewport" content="width=device-width, initial-scale=1.0">'
        }
        bodyTag.attributes {
            class = ${props.bodyClass}
            styles = Neos.Fusion:Loop {
                @if.hasStyles = ${Type.isArray(props.bodyStyles) && Array.length(props.bodyStyles)}
                @glue = ';'
                items = ${props.bodyStyles}
                itemRenderer = ${itemKey + ':' + item}
            }
        }
        body = Neos.Demo:Template.Base {
            header = ${props.header}
            heroContent = ${props.heroContent}
            heroImage = ${props.heroImage}
            content = ${props.content}
            footer  = ${props.footer}
        }
        @process.convertUris = Neos.Neos:ConvertUris
    }
}
