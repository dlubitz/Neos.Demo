/**
 * Render for the chapter overview
 */
prototype(Neos.Demo:Document.Book) < prototype(Neos.Demo:Document.LandingPage) {
    content >
    content = Neos.Fusion:Component {
        renderDummyImage = ${node.context.inBackend}
        content = Neos.Neos:ContentCollection {
            nodePath = 'main'
        }
        chapters = Neos.Fusion:Map {
            items = ${q(documentNode).children('[instanceof Neos.Demo:Document.Chapter]').get()}
            itemName = 'chapter'
            itemRenderer = Neos.Fusion:DataStructure {
                href = Neos.Neos:NodeUri {
                    node = ${chapter}
                }
                title = ${q(chapter).property('title')}
                description =  ${q(chapter).property('chapterDescription')}
                imageSrc = Neos.Neos:ImageUri {
                    @if.hasImage = ${q(chapter).property('chapterImage')}
                    asset = ${q(chapter).property('chapterImage')}
                    maximumWidth = 144
                }
                imageAlt = ${q(chapter).property('chapterImageAlt')}
            }
        }

        renderer = afx`<Neos.Demo:Presentation.ChapterMenu {...props} />`
    }
}
