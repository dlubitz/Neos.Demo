##
# "Registration" element based on Fusion.Form
#
prototype(Neos.Demo:Content.Registration) < prototype(Neos.Fusion.Form:Runtime.RuntimeForm) {
    namespace = "neos-demo-registration"

    process {
        content = afx`
            <fieldset class="space-y-6 mb-8 max-w-[300px]">
                <Neos.Fusion.Form:FieldContainer field.name="firstName" label="First Name" attributes.class="flex flex-col">
                    <Neos.Fusion.Form:Input attributes.class="form-input" />
                </Neos.Fusion.Form:FieldContainer>
                <Neos.Fusion.Form:FieldContainer field.name="lastName" label="Last Name" attributes.class="flex flex-col">
                    <Neos.Fusion.Form:Input attributes.class="form-input" />
                </Neos.Fusion.Form:FieldContainer>
                <Neos.Fusion.Form:FieldContainer field.name="username" label="Username" attributes.class="flex flex-col">
                    <Neos.Fusion.Form:Input attributes.class="form-input" />
                    <small>(lowercase letters and numbers only)</small>
                </Neos.Fusion.Form:FieldContainer>
                <Neos.Fusion.Form:FieldContainer field.name="password" label="Password" attributes.class="flex flex-col">
                    <Neos.Fusion.Form:Password attributes.class="form-input" />
                </Neos.Fusion.Form:FieldContainer>
            </fieldset>
        `

        schema {
            firstName = ${Form.Schema.string().isRequired()}
            lastName = ${Form.Schema.string().isRequired()}
            username = ${Form.Schema.string().isRequired()}
            password = ${Form.Schema.string().isRequired()}
        }
    }

    action {
        createBackendUser {
            type = 'Neos.Demo.Form.Runtime:CreateUser'
            options {
                firstName = ${data.firstName}
                lastName = ${data.lastName}
                username = ${data.username}
                password = ${data.password}
                // you can configure an array of Backend Roles to be added to this user
                roles = ${['Neos.Neos:Editor']}
                // this is basically for security reasons if someone
                // forgets to remove this functionality
                // you can configure a valid DateTime constructor format here
                expiry = '+1 week'
            }
            @position = 'start'
        }
        redirect {
            type = 'Neos.Fusion.Form.Runtime:Redirect'
            options.uri = Neos.Fusion:UriBuilder {
                package = 'Neos.Neos'
                controller = 'Login'
                action = 'index'
                additionalParams {
                    username = ${data.username}
                }
            }
            @position = 'end'
        }
    }
}
